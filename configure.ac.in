dnl
dnl usermon - A user monitor plugin for the Xfce panel
dnl
dnl 2003-2020 Fabrice Colin <fabrice.colin@gmail.com>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([usermon_version_major], [0])
m4_define([usermon_version_minor], [0])
m4_define([usermon_version_micro], [3])
m4_define([usermon_version_nano],  [])
m4_define([usermon_version_build], [@REVISION@])
m4_define([usermon_version_tag], [git])
m4_define([usermon_version], [usermon_version_major().usermon_version_minor().usermon_version_micro()ifelse(usermon_version_nano(), [], [], [.usermon_version_nano()])ifelse(usermon_version_tag(), [git], [usermon_version_tag()-usermon_version_build()], [usermon_version_tag()])])
m4_define([usermon_default_debug], [ifelse(usermon_version_tag(), [git], [yes], [minimum])])

AC_CONFIG_MACRO_DIR([m4])
dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright 2003-2020 Fabrice Colin. All rights reserved.])
AC_INIT([usermon], [usermon_version], [https://github.com/FabriceColin/usermon/issues], [usermon])
AC_PREREQ([2.50])
AC_REVISION([usermon_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.11 no-dist-gzip dist-bzip2 tar-ustar])
AC_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([0.35.0])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([stdlib.h unistd.h locale.h stdio.h errno.h time.h string.h \
                  math.h sys/types.h sys/wait.h memory.h signal.h sys/prctl.h \
                  libintl.h])
AC_CHECK_FUNCS([bind_textdomain_codeset])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([fr])

dnl *******************************
dnl *** Check for X11 installed ***
dnl *******************************
XDT_CHECK_LIBX11_REQUIRE()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBNOTIFY], [libnotify], [0.7.0])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([usermon_default_debug])
if test "x$enable_debug" = "xyes"; then
	if test "x$GCC" = "xyes"; then
		CFLAGS="$CFLAGS -ggdb3 -O0 -DDEBUG=1 -DDEBUG_TRACE=1"
	fi
else
	CFLAGS="$CFLAGS -DNDEBUG"
fi

dnl *********************************
dnl *** Substitute platform flags ***
dnl *********************************
AC_MSG_CHECKING([PLATFORM_CPPFLAGS])
AC_MSG_RESULT([$PLATFORM_CPPFLAGS])
AC_SUBST([PLATFORM_CPPFLAGS])
AC_MSG_CHECKING([PLATFORM_CFLAGS])
AC_MSG_RESULT([$PLATFORM_CFLAGS])
AC_SUBST([PLATFORM_CFLAGS])
AC_MSG_CHECKING([PLATFORM_LDFLAGS])
AC_MSG_RESULT([$PLATFORM_LDFLAGS])
AC_SUBST([PLATFORM_LDFLAGS])

AC_OUTPUT([
Makefile
icons/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
panel-plugin/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:    $enable_debug"
echo
